
// Вызов клиентского метода
&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	
	Если ИдентификаторКоманды = "БПЗаполнениеОбъектаИнвентаризацияПоСчету" Тогда
		Оповещение = Новый ОписаниеОповещения("ВыполнитьКомандуЗавершение", ЭтаФорма);
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Склад", ВладелецФормы.Объект.Склад);
		ОткрытьФорму("ВнешняяОбработка.БПЗаполнениеОбъектаИнвентаризацияПоСчету.Форма.ФормаОтбора", ПараметрыФормы, ВладелецФормы,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКомандуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ВходящиеПараметры = Новый Структура;
	ВходящиеПараметры.Вставить("Субконто1", 			Результат.Субконто1);
	ВходящиеПараметры.Вставить("Субконто2", 			Результат.Субконто2);
	ВходящиеПараметры.Вставить("Субконто3", 			Результат.Субконто3);
	ВходящиеПараметры.Вставить("Дата", 					ВладелецФормы.Объект.Дата);
	ВходящиеПараметры.Вставить("Организация", 			ВладелецФормы.Объект.Организация);
	ЗаполнитьОстаткиНаСервере(ВходящиеПараметры);
	
	ВладелецФормы.Объект.Товары.Очистить();
	Для Каждого СтрокаТовары Из ОстаткиТоваров Цикл
		НоваяСтрока = ВладелецФормы.Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
	КонецЦикла;
	ВладелецФормы.Модифицированность = Истина;
	
КонецПроцедуры // ВыполнитьКомандуЗавершение()

&НаСервере
Процедура ЗаполнитьОстаткиНаСервере(ВходящиеПараметры)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОстаткиТоваров.Загрузить(ОбработкаОбъект.ПолучитьОстаткиНаСкладе(ВходящиеПараметры));
КонецПроцедуры // ЗаполнитьОстаткиНаСвервере()

&НаКлиенте
Процедура Отладка(Команда)
	ВыполнитьКоманду("БПЗаполнениеОбъектаИнвентаризацияПоСчету", Неопределено);
КонецПроцедуры
